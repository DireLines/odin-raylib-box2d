package main

import "core:fmt"
import "core:strings"
import b2 "vendor:box2d"
import rl "vendor:raylib"


// This loads atlas.png (generated by the atlas builder) at compile time and stores it in the
// executable, the type of this constant will be `[]u8`. I.e. a slice of bytes. This means that you
// don't need atlas.png next to your game after compilation. It will live in the executable. When
// the executable starts it loads a raylib texture from this data.
ATLAS_DATA :: #load("atlas.png")
// This is loaded in `main` from `ATLAS_DATA`
atlas: rl.Texture


convert_world_to_screen :: proc(p: b2.Vec2, cv: ScreenConversion) -> rl.Vector2 {
	return {cv.scale * p.x + 0.5 * cv.screen_width, 0.5 * cv.screen_height - cv.scale * p.y}
}
get_texture :: proc(game: ^Game, filename: string) -> rl.Texture2D {
	texture, ok := game.textures[filename]
	if ok {
		return texture
	}
	return load_texture(game, filename)
}
load_texture :: proc(game: ^Game, filename: string) -> rl.Texture2D {
	texture := rl.LoadTexture(strings.clone_to_cstring(filename))
	game.textures[filename] = texture
	return texture
}
unload_texture :: proc(game: ^Game, filename: string) {
	texture, ok := game.textures[filename]
	if ok {
		rl.UnloadTexture(texture)
	}
}
draw_object :: proc(obj: GameObject) {
	p := b2.Body_GetWorldPoint(obj.body_id, {-0.5 * cv.tile_size, 0.5 * cv.tile_size})
	radians := b2.Body_GetRotation(obj.body_id)

	ps := convert_world_to_screen(p, cv)

	source := atlas_textures[obj.sprite.image].rect
	texture_scale := cv.tile_size * cv.scale / f32(source.width)
	dest := Rect{ps.x, ps.y, source.width * texture_scale, source.height * texture_scale}
	rl.DrawTexturePro(
		atlas,
		source,
		dest,
		{0, 0},
		-rl.RAD2DEG * b2.Rot_GetAngle(radians),
		obj.sprite.color,
	)
}
init_game :: proc(game: ^Game) {
	game.window_width = WINDOW_WIDTH
	game.window_height = WINDOW_HEIGHT
	game.objects = make_soa(#soa[dynamic]GameObject)

	//raylib init
	rl.SetTraceLogLevel(.NONE) //shup up
	rl.InitWindow(WINDOW_WIDTH, WINDOW_HEIGHT, "odin+raylib+box2d game template")
	rl.SetTargetFPS(60)
	rl.SetConfigFlags({.WINDOW_RESIZABLE})

	// Load atlas from ATLAS_DATA, which was stored in the executable at compile-time.
	atlas_image := rl.LoadImageFromMemory(".png", raw_data(ATLAS_DATA), i32(len(ATLAS_DATA)))
	atlas = rl.LoadTextureFromImage(atlas_image)
	rl.UnloadImage(atlas_image)
	// Set the shapes drawing texture, this makes rl.DrawRectangleRec etc use the atlas
	rl.SetShapesTexture(atlas, SHAPES_TEXTURE_RECT)

	//box2d init
	game.world_id = b2.CreateWorld(b2.DefaultWorldDef())
}
deinit_game :: proc(game: ^Game) {
	b2.DestroyWorld(game.world_id)
	for _, texture in game.textures {
		rl.UnloadTexture(texture)
	}
	rl.UnloadTexture(atlas)
	rl.CloseWindow()
}
add_object :: proc(game: ^Game, obj: GameObject) {
	append_soa(&game.objects, obj)
}

render :: proc(game: ^Game) {
	timer := timer()
	rl.BeginDrawing()
	defer rl.EndDrawing()
	darkgray := rl.Color{32, 32, 30, 255}
	rl.ClearBackground(darkgray)
	for &obj in game.objects {
		draw_object(obj)
	}
	if game.paused {
		rl.DrawText(
			"~~paused~~",
			(game.window_width - rl.MeasureText("~~paused~~", 20)) / 2,
			100,
			20,
			rl.LIGHTGRAY,
		)
	}
	timer->time("render")
}

start_game :: proc(game: ^Game) {
	initialize(game) //custom init logic
	for !rl.WindowShouldClose() {
		timer := timer()
		dt := rl.GetFrameTime()
		if rl.IsKeyPressed(rl.KeyboardKey.P) {
			game.paused = !game.paused
		}
		if !game.paused {
			b2.World_Step(game.world_id, dt, 8)
			contacts := b2.World_GetContactEvents(game.world_id)
			timer->time("physics")
			// if contacts.beginCount + contacts.hitCount + contacts.endCount > 0 {
			// 	fmt.println("contacts this frame:", contacts)
			// }
		}
		render(game)
	}
}
